unit CP.FeatureVectorConsumer;

{$IFDEF FPC}
{$MODE delphi}
{$ENDIF}

interface

uses
    Classes, SysUtils,
    CP.Def;

type
    { TFeatureVectorConsumer }

    TFeatureVectorConsumer = class(TObject)
    public
        constructor Create;
        destructor Destroy; override;
        procedure Consume(var features: TDoubleArray); virtual; abstract;
    end;

    { TChromaFilter }

    TChromaFilter = class(TFeatureVectorConsumer)
    private
        FBufferOffset: integer;
        FBufferSize: integer;
        FConsumer: TFeatureVectorConsumer;
        FCoefficients: TDoubleArray;
        FLength: integer;
        FBuffer: array of TDoubleArray;
        FResult: TDoubleArray;
    public
        property Consumer: TFeatureVectorConsumer read FConsumer write FConsumer;
    public
        constructor Create(coefficients: TDoubleArray; length: integer; Consumer: TFeatureVectorConsumer);
        destructor Destroy; override;
        procedure Reset;
        procedure Consume(var features: TDoubleArray); override;
    end;

    { TChromaNormalizer }

    TChromaNormalizer = class(TFeatureVectorConsumer)
    private
        FConsumer: TFeatureVectorConsumer;
    public
        constructor Create(Consumer: TFeatureVectorConsumer);
        destructor Destroy; override;
        procedure Reset;
        procedure Consume(var features: TDoubleArray); override;
    end;

    TChromaResampler = class(TFeatureVectorConsumer)
    private
        FResult: TDoubleArray;
        FConsumer: TFeatureVectorConsumer;
        FIteration: integer;
        FFactor: integer;
    public
        property Consumer: TFeatureVectorConsumer read FConsumer write FConsumer;
    public
        constructor Create(factor: integer; Consumer: TFeatureVectorConsumer);
        destructor Destroy; override;
        procedure Reset;
        procedure Consume(var features: TDoubleArray); override;

    end;

implementation

uses
    CP.Utils;

{ TChromaFilter }

constructor TChromaFilter.Create(coefficients: TDoubleArray; length: integer; Consumer: TFeatureVectorConsumer);
begin
    SetLength(FResult, 12);
    FCoefficients := coefficients;
    FLength := length; // = Length( coefficients)
    SetLength(FBuffer, 8);

    FBufferOffset := 0;
    FBufferSize := 1;
    FConsumer := Consumer;
end;



destructor TChromaFilter.Destroy;
begin
    SetLength(FResult, 0);
    FConsumer := nil;
    SetLength(FCoefficients, 0);
    inherited Destroy;
end;



procedure TChromaFilter.Reset;
begin
    FBufferSize := 1;
    FBufferOffset := 0;
end;



procedure TChromaFilter.Consume(var features: TDoubleArray);
var
    offset: integer;
    i, j: integer;

begin
    SetLength(FBuffer[FBufferOffset],Length(features));
    for i := 0 to Length(features) - 1 do
    begin
        FBuffer[FBufferOffset,i] := features[i]; // PDoulbe  = pointer auf ein double array
    end;
    FBufferOffset := (FBufferOffset + 1) mod 8;
    if (FBufferSize >= FLength) then
    begin
        offset := (FBufferOffset + 8 - FLength) mod 8;
        for i := 0 to length(FResult) - 1 do
        begin
            FResult[i] := 0.0;
        end;
        for i := 0 to 11 do
        begin
            for j := 0 to FLength - 1 do
            begin

                FResult[i] := FResult[i] + FBuffer[(offset + j) mod 8,i] * FCoefficients[j];
            end;
        end;
        FConsumer.Consume(FResult);
    end
    else
    begin
        Inc(FBufferSize);
    end;
end;

{ TFeatureVectorConsumer }

constructor TFeatureVectorConsumer.Create;
begin

end;



destructor TFeatureVectorConsumer.Destroy;
begin
    inherited Destroy;
end;

{ TChromaNormalizer }

constructor TChromaNormalizer.Create(Consumer: TFeatureVectorConsumer);
begin
    FConsumer := Consumer;
end;



destructor TChromaNormalizer.Destroy;
begin
    FConsumer := nil;
    inherited Destroy;
end;



procedure TChromaNormalizer.Reset;
begin
    { nothing to do }
end;



procedure TChromaNormalizer.Consume(var features: TDoubleArray);
var
    norm: double;
begin
    norm:=  EuclideanNorm(features);
    NormalizeVector(features,norm , 0.01);
    FConsumer.Consume(features);
end;



constructor TChromaResampler.Create(factor: integer; Consumer: TFeatureVectorConsumer);
begin
    SetLength(FResult, 12);
    Reset;
    FFactor := factor;
    FConsumer := Consumer;
end;



destructor TChromaResampler.Destroy;
begin
    SetLength(FResult, 0);
    inherited Destroy;
end;



procedure TChromaResampler.Reset;
var
    i: integer;
begin
    FIteration := 0;
    for i := 0 to length(FResult) - 1 do
    begin
        FResult[i] := 0.0;
    end;
end;



procedure TChromaResampler.Consume(var features: TDoubleArray);
var
    i: integer;
begin
    for i := 0 to 11 do
    begin
        FResult[i] := FResult[i] + features[i];
    end;
    Inc(FIteration);
    if (FIteration = FFactor) then
    begin
        for i := 0 to 11 do
        begin
            FResult[i] := FResult[i] / FFactor;
        end;
        FConsumer.Consume(FResult);
        Reset();
    end;
end;

end.
