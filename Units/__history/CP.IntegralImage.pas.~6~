unit CP.IntegralImage;
{$IFDEF FPC}
{$MODE delphi}
{$ENDIF}

interface

uses
    Classes, SysUtils,
    CP.Image;

{ *
  * Image transformation that allows us to quickly calculate the sum of
  * values in a rectangular area.
  *
  * http://en.wikipedia.org/wiki/Summed_area_table
  * }
type

    { TIntegralImage }

    TIntegralImage = class(TObject)
    private
        FImage: TImage;
        procedure Transform;
    public
        { * Construct the integral image. Note that will modify the original
          * image in-place, so it will not be usable afterwards. }
        constructor Create(Image: TImage);
        destructor Destroy; override;
        function Area(x1, y1, x2, y2: integer): double;
        function NumColumns: integer;
        function NumRows: integer;
        function GetData(Row, Column: integer): double;
    end;

implementation

{ TIntegralImage }

procedure TIntegralImage.Transform;
var
    lColumns, lRows: integer;
    m, n: integer;
begin
    // c++ is a pain;
    // in Pascal you know what you're doing
    lColumns := FImage.NumColumns;
    lRows := FImage.NumRows;
    for m := 1 to lColumns - 1 do
    begin
        // First column - add value on top
        FImage.SetData(0, m, FImage.GetData(0, m) + FImage.GetData(0, m - 1));
    end;
    for n := 1 to lRows - 1 do
    begin
        // First row - add value on left
        FImage.SetData(n, 0, FImage.GetData(n, 0) + FImage.GetData(n - 1, 0));
        for m := 1 to lColumns - 1 do
        begin
            FImage.SetData(n, m, FImage.GetData(n, m) + FImage.GetData(n - 1, m) + FImage.GetData(n, m - 1) - FImage.GetData(n - 1, m - 1));
        end;
    end;
end;

constructor TIntegralImage.Create(Image: TImage);
begin
    FImage := Image;
    Transform;
end;

destructor TIntegralImage.Destroy;
begin
    FImage := nil;
    inherited;
end;

function TIntegralImage.GetData(Row, Column: integer): double;
begin
    result := FImage.GetData(Row, Column);
end;

function TIntegralImage.Area(x1, y1, x2, y2: integer): double;
var
    lArea: double;
begin
    if (x2 < x1) or (y2 < y1) then
    begin
        result := 0.0;
        Exit;
    end;
    lArea := FImage.GetData(x2, y2);
    if (x1 > 0) then
    begin
        lArea := lArea - FImage.GetData(x1 - 1, y2);
        if (y1 > 0) then
        begin
            lArea := lArea + FImage.GetData(x1 - 1, y1 - 1);
        end;
    end;
    if (y1 > 0) then
    begin
        lArea := lArea - FImage.GetData(x2, y1 - 1);
    end;
    result := lArea;
end;

function TIntegralImage.NumColumns: integer;
begin
    result := FImage.NumColumns;
end;

function TIntegralImage.NumRows: integer;
begin
    result := FImage.NumRows;
end;

end.
